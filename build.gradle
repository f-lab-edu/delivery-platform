plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'checkstyle'
}

subprojects {
    group 'org.flab.deliveryplatform'
    sourceCompatibility = '11'

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'checkstyle'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    checkstyle {
        toolVersion = '10.3'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        showViolations = true
        ignoreFailures = false
        maxWarnings = 0
    }

    test {
        useJUnitPlatform()
    }

    task unitTest(type: Test) {
        exclude('org/flab/deliveryplatform/server/**')
        useJUnitPlatform()
    }
}

task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}
